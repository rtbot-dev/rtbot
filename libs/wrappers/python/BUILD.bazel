load("@pybind11_bazel//:build_defs.bzl", "pybind_extension")
load("@rules_python//python:packaging.bzl", "py_package", "py_wheel")
load("//tools/generator:generator.bzl", "rtbot_generate")
load("@aspect_bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")


package(default_visibility = ["//visibility:public"])

rtbot_generate(
    name = "generated",
    target = "python",
)

pybind_extension(
    # recall that this doesn't collide with the py_library name
    # and in fact it has to be the same name!
    name = "rtbotapi", 
    srcs = ["rtbot.cpp"],
    visibility = ["//visibility:public"],
    deps = ["//libs/api:rtbot-api"],
)

py_library(
    name = "rtbotapi",
    data = [":rtbotapi.so", "rtbotx.py"],
)

py_package(
    name = "rtbot",
    deps = [":rtbotapi", ":generated"],
)

copy_to_directory(
    name = "copy",
    srcs = [":rtbot", ":generated"],
    # while compiling the py extension succeeds in windows
    # the produced artifact has a wrong .so extension
    # here we fix that 
    # (rtbotapi.so should be rtbotapi.pyd in windows)
    replace_prefixes = select({
        "@platforms//os:windows": {
            "generated/jsonschema.py": "operators.py",
            "rtbot.py": "__init__.py",
            "rtbotapi.so": "rtbotapi.pyd",
        },
        "@platforms//os:macos": {
            "generated/jsonschema.py": "operators.py",
            "rtbot.py": "__init__.py",
            "rtbotapi.so": "rtbotapi.so",
        },
        "@platforms//os:linux": {
            "generated/jsonschema.py": "operators.py",
            "rtbot.py": "__init__.py",
            "rtbotapi.so": "rtbotapi.so",
        }
    }),
    out = "rtbot"
)

# Package just a specific py_libraries, without their dependencies
py_wheel(
    name = "rtbot_wheel",
    distribution = "rtbot",
    python_tag = "py3",
    stamp = 1,
    version = "{VERSION}",
    strip_path_prefixes = [
        "libs/wrappers/python",
    ],
    platform = select({
        "@platforms//os:windows": "win_amd64",
        # TODO: add mac arm
        "@platforms//os:macos": "macosx_10_7_x86_64",
        "@platforms//os:linux": "manylinux2014_x86_64",
    }),
    deps = [":copy"]
)

py_test(
    name = "rtbot_test",
    srcs = ["rtbot_test.py"],
    data = [
        ":copy"
    ],
)
