load("@pybind11_bazel//:build_defs.bzl", "pybind_extension")
load("@rules_python//python:packaging.bzl", "py_package", "py_wheel")
load("//tools/generator:generator.bzl", "rtbot_generate")
load("@aspect_bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@py_deps//:requirements.bzl", "requirement")

package(default_visibility = ["//visibility:public"])

rtbot_generate(
    name = "generated",
    target = "python",
)

pybind_extension(
    name = "rtbotapi",
    srcs = ["rtbot.cpp"],
    visibility = ["//visibility:public"],
    deps = ["//libs/api:rtbot-api"],
)

py_library(
    name = "py_rtbotapi",
    data = [
        "rtbot.py",
        ":rtbotapi.so",
    ],
)

py_package(
    name = "rtbot",
    deps = [
        ":generated",
        ":py_rtbotapi",
    ],
)

copy_to_directory(
    name = "copy",
    srcs = [
        "MANIFEST.in",
        "README.md",
        "setup.py",
        ":generated",
        ":rtbot",
    ],
    out = "rtbot",
    exclude_srcs_patterns = ["libs/api/proto/**/*"],
    # while compiling the py extension succeeds in windows
    # the produced artifact has a wrong .so extension
    # here we fix that
    # (rtbotapi.so should be rtbotapi.pyd in windows)
    replace_prefixes = select({
        "@platforms//os:windows": {
            "generated/jsonschema.py": "operators.py",
            "rtbot.py": "__init__.py",
            "rtbotapi.so": "rtbotapi.pyd",
        },
        "@platforms//os:macos": {
            "generated/jsonschema.py": "operators.py",
            "rtbot.py": "__init__.py",
            "rtbotapi.so": "rtbotapi.so",
        },
        "@platforms//os:linux": {
            "generated/jsonschema.py": "operators.py",
            "rtbot.py": "__init__.py",
            "rtbotapi.so": "rtbotapi.so",
        },
    }),
)

# Use global version from root BUILD.bazel

py_wheel(
    name = "rtbot_wheel_base",
    distribution = "rtbot",
    python_tag = "py3",
    stamp = 1,
    strip_path_prefixes = [
        "libs/wrappers/python",
    ],
    version = "{RTBOT_VERSION}",  # Dynamic version from workspace status
    deps = [":copy"],
)

genrule(
    name = "rtbot_wheel",
    srcs = [":rtbot_wheel_base", "//:version"],
    outs = ["rtbot.whl"],
    cmd = "cp $(location :rtbot_wheel_base) $(location rtbot.whl)",
    stamp = 1,
)

py_test(
    name = "rtbot_test",
    srcs = ["rtbot_test.py"],
    data = [
        ":copy",
    ],
    deps = [
        requirement("pandas"),
    ],
)
