load("@npm//:vite/package_json.bzl", "bin")
load("@aspect_rules_js//js:defs.bzl", "js_run_devserver")
load("@aspect_rules_js//npm:defs.bzl", "npm_link_package", "npm_package")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@aspect_bazel_lib//lib:copy_directory.bzl", "copy_directory")
load("@aspect_bazel_lib//lib:copy_to_bin.bzl", "copy_to_bin")
load("@aspect_bazel_lib//lib:copy_file.bzl", "copy_file")
load("@aspect_bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")

npm_link_all_packages()

genrule(
    name = "package_json",
    outs = ["package.json"],
    cmd = """
            echo "{
    \\"name\\": \\"@rtbot/core\\",
    \\"version\\": \\"0.1.0\\",
    \\"main\\": \\"./wasm/bindings-cpp-wasm.js\\"
}" > $@
    """,
)

copy_file(
    name = "public-wasm",
    src = "@rtbot//libs/core/lib:rtbot-wasm/bindings-cpp-wasm.wasm",
    out = "public/src/api/rtbot/bindings-cpp-wasm.wasm",
)

copy_file(
    name = "public-wasm-release",
    src = "@rtbot//libs/core/lib:rtbot-wasm/bindings-cpp-wasm.wasm",
    out = "public/assets/bindings-cpp-wasm.wasm",
)

# npm package for wasm code
copy_to_directory(
    name = "wasm",
    srcs = [
        "@rtbot//libs/core/lib:rtbot-wasm",
    ],
    include_external_repositories = ["*"],
    replace_prefixes = {
        "libs/core/lib/rtbot-wasm": ".",
    },
)

npm_package(
    name = "rtbot",
    srcs = [
        ":package_json",
        ":wasm",
    ],
    package = "@rtbot/core",
)

npm_link_package(
    name = "node_modules/@rtbot/core",
    src = ":rtbot",
)

_DEPS = glob([
    "public/**/*",
    "src/**/*",
    "*.cjs",
    "*.js",
]) + [
    ":public-wasm",
    "index.html",
    "vite.config.ts",
    "tsconfig.json",
    "tsconfig.node.json",
] + [
    "//:node_modules/@rtbot/core",
    "//:node_modules/comlink",
    "//:node_modules/daisyui",
    "//:node_modules/react",
    "//:node_modules/react-icons",
    "//:node_modules/react-color",
    "//:node_modules/reactflow",
    "//:node_modules/date-fns",
    "//:node_modules/papaparse",
    "//:node_modules/react-plotly.js",
    "//:node_modules/nanoid",
    "//:node_modules/react-dom",
    "//:node_modules/react-router-dom",
    "//:node_modules/firebase",
    "//:node_modules/firebase-tools",
    "//:node_modules/lottie-react",
    "//:node_modules/rxjs",
    "//:node_modules/tailwindcss",
    "//:node_modules/autoprefixer",
    "//:node_modules/@headlessui/react",
    "//:node_modules/zod",
    "//:node_modules/@types/react",
    "//:node_modules/@types/react-dom",
    "//:node_modules/@vitejs/plugin-react-swc",
    "//:node_modules/typescript",
    "//:node_modules/vite",
    "//:node_modules/vite-tsconfig-paths",
]

js_run_devserver(
    name = "dev",
    args = ["--host"],
    command = "node_modules/.bin/vite",
    data = _DEPS,
    tags = ["ibazel_notify_changes"],
)

bin.vite_binary(
    name = "run",
    args = ["--host"],
    chdir = package_name(),
    data = _DEPS,
    tags = ["ibazel_notify_changes"],
)

bin.vite_binary(
    name = "build",
    args = ["build"],
    chdir = package_name(),
    data = _DEPS + ["public-wasm-release"],
)
